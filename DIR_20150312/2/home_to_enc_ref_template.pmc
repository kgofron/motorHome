; Homing PLC for Axis 2 (PLC 9)

; This homing routine will move to a limit, and then search for the encoder
; reference mark.
; The axis home velocity sign sets the direction for the final "home to reference
; mark" move. The initial move to the limit will be in the opposite direction.
; 
; Wayne Lewis 20140108
; 
; Macros:
; - PLC: PLC number
; - AXIS: motion axis
; - TIMER: PMAC timer to use
; - SERVO: first three digits of axis gate array (I7mn)

; P variables
; P900 - homing state (homing, not homing)
; P901 - homing status (limit search, home search, post-home move)
; P902 - post-home move (0 = return to previous, 1 = stay at zero)

; Homing State P Variable
P900 = 0

; Homing Status P Variable
P901 = 0

DEL GAT
OPEN PLC9 CLEAR
P901 = 1

;---- Configuring State ----
P900=1
;Save high soft limits to P variable Pxx04
P904=I213
;Save the low soft limits to P variable Pxx20
P920=I214
;Save the home capture 2lags to P variable Pxx36
P936=I7022
;Save the limit flags to P variable Pxx68
P968=I224
;Save the current position to P variable Pxx84
P984=M262
;Clear the soft limits
I213=0
I214=0

if (P901 = 1)

  ;---- FastSearch State ----
  ; Move to the limit switch
	if (P901=1)
		P900=8
		; Execute the move commands
		M272=100000000*(-I223/ABS(I223))
		cmd "#2J^*"
		; Wait for the move to complete
		I5512 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5512 > 0)
		endw
		I5512 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M240=0) ; At least one motor should not be In Position
		and (M242=0) ; No following errors should be set for any motor
		and (I5512 > 0) ; Check for timeout
		and (P901 = 1) ; Check that we didn't abort
		endw

		; Check why we left the while loop
		if (M242=1) ; If a motor hit a following error
			P901 = 4
		endif
		if (I5512<0 or I5512=0) ; If we timed out
			P901 = 3
		endif
	endif

	;---- Store the difference between current pos and start pos ----
	if (P901=1)
		P986=(P986-M262)/(I208*32)-(I226/16)
	endif

	;---- Homing State ----
	if (P901=1)
		P900=4
    ; Set the capture flag 
    I7022=1
		; Execute the move commands
		cmd "#2hm"
		; Wait for the move to complete
		I5512 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5512 > 0)
		endw
		I5512 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M240=0) ; At least one motor should not be In Position
		and (M242=0) ; No following errors should be set for any motor
		and (M230=0) ; Should not stop on position limit for selected motors
		and (I5512 > 0) ; Check for timeout
		and (P901 = 1) ; Check that we didn't abort
		endw
		; Check why we left the while loop
		if (M242=1) ; If a motor hit a following error
			P901 = 4
		endif
		if (M230=1) ; If a motor hit a limit
			P901 = 5
		endif
		if (I5512<0 or I5512=0) ; If we timed out
			P901 = 3
		endif
	endif

	;---- Check if all motors have homed ----
	if (P901=1)
	and (M245=0)
		P901=6
	endif

	;---- PostHomeMove State ----
	if (P901=1)
    ; Check the type of post-home move
    if (P902 = 0)
      P900=5
      ; Execute the move commands
      M272=P984/(I208*32)
      cmd "#2J=*"
      ; Wait for the move to complete
      I5512 = 20 * 8388608/I10 ; Small delay to start moving
      while (I5512 > 0)
      endw
      I5512 = 600000 * 8388608/I10 ; Now start checking the conditions
      while (M240=0) ; At least one motor should not be In Position
      and (M242=0) ; No following errors should be set for any motor
      and (M230=0) ; Should not stop on position limit for selected motors
      and (I5512 > 0) ; Check for timeout
      and (P901 = 1) ; Check that we didn't abort
      endw
      ; Check why we left the while loop
      if (M242=1) ; If a motor hit a following error
        P901 = 4
      endif
      if (M230=1) ; If a motor hit a limit
        P901 = 5
      endif
      if (I5512<0 or I5512=0) ; If we timed out
        P901 = 3
      endif
    endif
	endif

endif

;---- Done ----
if (P901 = 1)
	;If we've got this far without failing, set status and state done
	P901=0
	P900=7
endif

;---- Tidy Up ----
;Stop all motors if they don't have a following error
if (M242=0)
	cmd "#2J/"
endif
;Restore the high soft limits from P variables Pxx04..x19
I213=P904
;Restore the low soft limits from P variables Pxx20..x35
I214=P920
;Restore the home capture flags from P variables Pxx36..x51
I7022=P936
;Restore the limit flags to P variables Pxx68..x83
I224=P968

DISABLE PLC9
CLOSE


