; Homing PLC for Axis 4 (PLC 11)
; 
; This program will home an axis to either the positive or negative limit. The
; homing program performs the following steps:
; 1. Move to the limit switch being used as a reference.
; 2. Perform a homing move away from the limit switch until it deactivates.
; 3. Return to the starting position. 
; 
; Note that the direction set by the home velocity (Ixx23) is opposite to the
; limit switch actually used for homing. This is because the homing movement
; (Step 2 above) is away from the limit, with the controller looking for the
; switch to deactivate to indicate the home position. This is summarised below:
; Home at negative limit -> Positive home velocity
; Home at positive limit -> Negative home velocity
; 
; Wayne Lewis 20140108
; 
; Macros:
; - PLC: PLC number
; - AXIS: motion axis
; - TIMER: PMAC timer to use
; - SERVO: first three digits of axis gate array (I7mn)

; P variables
; P1100 - homing state (homing, not homing)
; P1101 - homing status (limit search, home search, post-home move)
; P1102 - post-home move (0 = return to previous, 1 = stay at zero)

; Initial configuration of state variables.
; Homing State P Variable
P1100 = 0

; Homing Status P Variable
P1101 = 0

DEL GAT
OPEN PLC11 CLEAR
P1101 = 1

;---- Configuring State ----
P1100=1
;Save high soft limits to P variables Pxx04..x19
P1104=I413
;Save the low soft limits to P variables Pxx20..x35
P1120=I414
;Save the home capture flags to P variables Pxx36..x51
P1136=I7042
;Store value to use in moving off a flag in P variables Pxx52..x67
P1152=10
;Save the limit flags to P variables Pxx68..x83
P1168=I424
;Save the current position to P variables Pxx84..x99
P1184=M462
;Clear the soft limits
I413=0
I414=0

if (P1101=1)

	;---- FastSearch State ----
  ; Move to the limit switch
	if (P1101=1)
		P1100=8
		; Execute the move commands
		M472=100000000*(-I423/ABS(I423))
		cmd "#4J:*"
		; Wait for the move to complete
		I5612 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5612 > 0)
		endw
		I5612 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M440=0) ; At least one motor should not be In Position
		and (M442=0) ; No following errors should be set for any motor
		and (I5612 > 0) ; Check for timeout
		and (P1101 = 1) ; Check that we didn't abort
		endw

		; Check why we left the while loop
		if (M442=1) ; If a motor hit a following error
			P1101 = 4
		endif
		if (I5612<0 or I5612=0) ; If we timed out
			P1101 = 3
		endif
	endif

	;---- Store the difference between current pos and start pos ----
	if (P1101=1)
		P1186=(P1186-M462)/(I408*32)-(I426/16)
	endif

	;---- Homing State ----
	if (P1101=1)
		P1100=4
    ; Set the capture flag correctly            
    ; If homing direction is negative, we are homing using the positive
    ; limit switch and vice versa.              
    if (I423<0)
      I7043=1
    else
      I7043=2
    endif             
		; Execute the move commands
		I7042=P1152
		cmd "#4hm"
		; Wait for the move to complete
		I5612 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5612 > 0)
		endw
		I5612 = 600000 * 8388608/I10 ; Now start checking the conditions
    P1105 = 0
		while (M440=0) ; At least one motor should not be In Position
		and (M442=0) ; No following errors should be set for any motor
		and (P1105=0) ; Should not stop on position limit for selected motors
		and (I5612 > 0) ; Check for timeout
		and (P1101 = 1) ; Check that we didn't abort
    if (I423 < 0 and M432 = 1)
      P1105 = 1
    endif
    if (I423 > 0 and M431 = 1)
      P1105 = 1
    endif
		endw
		; Check why we left the while loop
		if (M442=1) ; If a motor hit a following error
			P1101 = 4
		endif
		if (M430=1) ; If a motor hit a limit
			P1101 = 5
		endif
		if (I5612<0 or I5612=0) ; If we timed out
			P1101 = 3
		endif
	endif

	;---- Check if all motors have homed ----
	if (P1101=1)
	and (M445=0)
		P1101=6
	endif

	;---- PostHomeMove State ----
	if (P1101=1)
    ; Check the type of post-home move
    if (P1102 = 0)
      P1100=5
      ; Execute the move commands
      M472=P1184/(I408*32)
      cmd "#4J=*"
      ; Wait for the move to complete
      I5612 = 20 * 8388608/I10 ; Small delay to start moving
      while (I5612 > 0)
      endw
      I5612 = 600000 * 8388608/I10 ; Now start checking the conditions
      while (M440=0) ; At least one motor should not be In Position
      and (M442=0) ; No following errors should be set for any motor
      and (M430=0) ; Should not stop on position limit for selected motors
      and (I5612 > 0) ; Check for timeout
      and (P1101 = 1) ; Check that we didn't abort
      endw
      ; Check why we left the while loop
      if (M442=1) ; If a motor hit a following error
        P1101 = 4
      endif
      if (M430=1) ; If a motor hit a limit
        P1101 = 5
      endif
      if (I5612<0 or I5612=0) ; If we timed out
        P1101 = 3
      endif
    endif
	endif

endif

;---- Done ----
if (P1101 = 1)
	;If we've got this far without failing, set status and state done
	P1101=0
	P1100=7
endif

;---- Tidy Up ----
;Stop all motors if they don't have a following error
if (M442=0)
	cmd "#4J/"
endif
;Restore the high soft limits from P variables Pxx04..x19
I413=P1104
;Restore the low soft limits from P variables Pxx20..x35
I414=P1120
;Restore the home capture flags from P variables Pxx36..x51
I7042=P1136
;Restore the limit flags to P variables Pxx68..x83
I424=P1168

DISABLE PLC11
CLOSE


