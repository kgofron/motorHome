; Homing PLC for Axis 5 (PLC 12)

; This homing routine will move to a limit, and then search for the home switch.
; The axis home velocity sign sets the direction for the final "home to
; home switch" move. The initial move to the limit will be in the opposite direction.
; 
; Wayne Lewis 20140108
; 
; Macros:
; - PLC: PLC number
; - AXIS: motion axis
; - TIMER: PMAC timer to use
; - SERVO: first three digits of axis gate array (I7mn)

; P variables
; P1200 - homing state (homing, not homing)
; P1201 - homing status (limit search, home search, post-home move)
; P1202 - post-home move (0 = return to previous, 1 = stay at zero)
; Homing State P Variable

P1200 = 0

; Homing Status P Variable
P1201 = 0

DEL GAT
OPEN PLC12 CLEAR
P1201 = 1

;---- Configuring State ----
P1200=1
;Save high soft limits to P variable Pxx04
P1204=I513
;Save the low soft limits to P variable Pxx20
P1220=I514
;Save the home capture flags to P variable Pxx36
P1236=I7112
;Save the limit flags to P variable Pxx68
P1268=I524
;Save the current position to P variable Pxx84
P1284=M562
;Clear the soft limits
I513=0
I514=0

if (P1201=1)
	;---- FastSearch State ----
  ; Move to the limit switch
	if (P1201=1)
		P1200=8
		; Execute the move commands
		M572=100000000*(-I523/ABS(I523))
		cmd "#5J^*"
		; Wait for the move to complete
		I5711 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5711 > 0)
		endw
		I5711 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M540=0) ; At least one motor should not be In Position
		and (M542=0) ; No following errors should be set for any motor
		and (I5711 > 0) ; Check for timeout
		and (P1201 = 1) ; Check that we didn't abort
		endw

		; Check why we left the while loop
		if (M542=1) ; If a motor hit a following error
			P1201 = 4
		endif
		if (I5711<0 or I5711=0) ; If we timed out
			P1201 = 3
		endif
	endif

	;---- Store the difference between current pos and start pos ----
	if (P1201=1)
		P1286=(P1286-M562)/(I508*32)-(I526/16)
	endif

	;---- Homing State ----
	if (P1201=1)
		P1200=4
    ; Set the capture flag to use the home flag.            
    I7113=0
    ; Set the polarity of the home switch. NO=2, NC=10.
		I7112=$(HOME_POL)
		; Execute the move commands
		cmd "#5hm"
		; Wait for the move to complete
		I5711 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5711 > 0)
		endw
		I5711 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M540=0) ; At least one motor should not be In Position
		and (M542=0) ; No following errors should be set for any motor
		and (M530=0) ; Should not stop on position limit for selected motors
		and (I5711 > 0) ; Check for timeout
		and (P1201 = 1) ; Check that we didn't abort
		endw
		; Check why we left the while loop
		if (M542=1) ; If a motor hit a following error
			P1201 = 4
		endif
		if (M530=1) ; If a motor hit a limit
			P1201 = 5
		endif
		if (I5711<0 or I5711=0) ; If we timed out
			P1201 = 3
		endif
	endif

	;---- Check if all motors have homed ----
	if (P1201=1)
	and (M545=0)
		P1201=6
	endif

	;---- PostHomeMove State ----
	if (P1201=1)
    ; Check the type of post-home move
	  if (P1202=0)
      P1200=5
      ; Execute the move commands
      M572=P1284/(I508*32)
      cmd "#5J=*"
      ; Wait for the move to complete
      I5711 = 20 * 8388608/I10 ; Small delay to start moving
      while (I5711 > 0)
      endw
      I5711 = 600000 * 8388608/I10 ; Now start checking the conditions
      while (M540=0) ; At least one motor should not be In Position
      and (M542=0) ; No following errors should be set for any motor
      and (M530=0) ; Should not stop on position limit for selected motors
      and (I5711 > 0) ; Check for timeout
      and (P1201 = 1) ; Check that we didn't abort
      endw
      ; Check why we left the while loop
      if (M542=1) ; If a motor hit a following error
        P1201 = 4
      endif
      if (M530=1) ; If a motor hit a limit
        P1201 = 5
      endif
      if (I5711<0 or I5711=0) ; If we timed out
        P1201 = 3
      endif
    endif
	endif

endif

;---- Done ----
if (P1201 = 1)
	;If we've got this far without failing, set status and state done
	P1201=0
	P1200=7
endif

;---- Tidy Up ----
;Stop all motors if they don't have a following error
if (M542=0)
	cmd "#5J/"
endif
;Restore the high soft limits from P variables Pxx04..x19
I513=P1204
;Restore the low soft limits from P variables Pxx20..x35
I514=P1220
;Restore the home capture flags from P variables Pxx36..x51
I7112=P1236
;Restore the limit flags to P variables Pxx68..x83
I524=P1268

DISABLE PLC12
CLOSE


