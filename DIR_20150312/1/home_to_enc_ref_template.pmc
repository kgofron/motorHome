; Homing PLC for Axis 1 (PLC 8)

; This homing routine will move to a limit, and then search for the encoder
; reference mark.
; The axis home velocity sign sets the direction for the final "home to reference
; mark" move. The initial move to the limit will be in the opposite direction.
; 
; Wayne Lewis 20140108
; 
; Macros:
; - PLC: PLC number
; - AXIS: motion axis
; - TIMER: PMAC timer to use
; - SERVO: first three digits of axis gate array (I7mn)

; P variables
; P800 - homing state (homing, not homing)
; P801 - homing status (limit search, home search, post-home move)
; P802 - post-home move (0 = return to previous, 1 = stay at zero)

; Homing State P Variable
P800 = 0

; Homing Status P Variable
P801 = 0

DEL GAT
OPEN PLC8 CLEAR
P801 = 1

;---- Configuring State ----
P800=1
;Save high soft limits to P variable Pxx04
P804=I113
;Save the low soft limits to P variable Pxx20
P820=I114
;Save the home capture 2lags to P variable Pxx36
P836=I7012
;Save the limit flags to P variable Pxx68
P868=I124
;Save the current position to P variable Pxx84
P884=M162
;Clear the soft limits
I113=0
I114=0

if (P801 = 1)

  ;---- FastSearch State ----
  ; Move to the limit switch
	if (P801=1)
		P800=8
		; Execute the move commands
		M172=100000000*(-I123/ABS(I123))
		cmd "#1J^*"
		; Wait for the move to complete
		I5511 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5511 > 0)
		endw
		I5511 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M140=0) ; At least one motor should not be In Position
		and (M142=0) ; No following errors should be set for any motor
		and (I5511 > 0) ; Check for timeout
		and (P801 = 1) ; Check that we didn't abort
		endw

		; Check why we left the while loop
		if (M142=1) ; If a motor hit a following error
			P801 = 4
		endif
		if (I5511<0 or I5511=0) ; If we timed out
			P801 = 3
		endif
	endif

	;---- Store the difference between current pos and start pos ----
	if (P801=1)
		P886=(P886-M162)/(I108*32)-(I126/16)
	endif

	;---- Homing State ----
	if (P801=1)
		P800=4
    ; Set the capture flag 
    I7012=1
		; Execute the move commands
		cmd "#1hm"
		; Wait for the move to complete
		I5511 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5511 > 0)
		endw
		I5511 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M140=0) ; At least one motor should not be In Position
		and (M142=0) ; No following errors should be set for any motor
		and (M130=0) ; Should not stop on position limit for selected motors
		and (I5511 > 0) ; Check for timeout
		and (P801 = 1) ; Check that we didn't abort
		endw
		; Check why we left the while loop
		if (M142=1) ; If a motor hit a following error
			P801 = 4
		endif
		if (M130=1) ; If a motor hit a limit
			P801 = 5
		endif
		if (I5511<0 or I5511=0) ; If we timed out
			P801 = 3
		endif
	endif

	;---- Check if all motors have homed ----
	if (P801=1)
	and (M145=0)
		P801=6
	endif

	;---- PostHomeMove State ----
	if (P801=1)
    ; Check the type of post-home move
    if (P802 = 0)
      P800=5
      ; Execute the move commands
      M172=P884/(I108*32)
      cmd "#1J=*"
      ; Wait for the move to complete
      I5511 = 20 * 8388608/I10 ; Small delay to start moving
      while (I5511 > 0)
      endw
      I5511 = 600000 * 8388608/I10 ; Now start checking the conditions
      while (M140=0) ; At least one motor should not be In Position
      and (M142=0) ; No following errors should be set for any motor
      and (M130=0) ; Should not stop on position limit for selected motors
      and (I5511 > 0) ; Check for timeout
      and (P801 = 1) ; Check that we didn't abort
      endw
      ; Check why we left the while loop
      if (M142=1) ; If a motor hit a following error
        P801 = 4
      endif
      if (M130=1) ; If a motor hit a limit
        P801 = 5
      endif
      if (I5511<0 or I5511=0) ; If we timed out
        P801 = 3
      endif
    endif
	endif

endif

;---- Done ----
if (P801 = 1)
	;If we've got this far without failing, set status and state done
	P801=0
	P800=7
endif

;---- Tidy Up ----
;Stop all motors if they don't have a following error
if (M142=0)
	cmd "#1J/"
endif
;Restore the high soft limits from P variables Pxx04..x19
I113=P804
;Restore the low soft limits from P variables Pxx20..x35
I114=P820
;Restore the home capture flags from P variables Pxx36..x51
I7012=P836
;Restore the limit flags to P variables Pxx68..x83
I124=P868

DISABLE PLC8
CLOSE


